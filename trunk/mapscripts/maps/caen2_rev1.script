game_manager
{       
        spawn
        {
		// mortis: mg clip exploit fix
		create
		{
			scriptName "bugfix1"
			classname "func_fakebrush"
			origin "-1936 384 232"
			contents 1  // CONTENTS_SOLID
			mins "-32 -40 -128"
			maxs "32 96 192"
		}

		// Ragnar: South pillar - fakebrush 
		create 
		{ 
			scriptname "pillar_fix1" 
			classname "func_fakebrush" 
			contents 65536 // CONTENTS_PLAYERCLIP 
			origin "-583 810 364" 
			mins "-20 -20 -92" 
			maxs "20 20 92" 
		} 

		// Ragnar: Second pillar from south - fakebrush 
		create 
		{ 
			scriptname "pillar_fix2" 
			classname "func_fakebrush" 
			contents 65536 // CONTENTS_PLAYERCLIP 
			origin "-583 1002 364" 
			mins "-20 -20 -92" 
			maxs "20 20 92" 
		} 

		// Ragnar: Second pillar from north - fakebrush 
		create 
		{ 
			scriptname "pillar_fix3" 
			classname "func_fakebrush" 
			contents 65536 // CONTENTS_PLAYERCLIP 
			origin "-583 1186 364" 
			mins "-20 -20 -92" 
			maxs "20 20 92" 
		} 

		// Ragnar: North pillar - fakebrush 
		create 
		{ 
			scriptname "pillar_fix4" 
			classname "func_fakebrush" 
			contents 65536 // CONTENTS_PLAYERCLIP 
			origin "-583 1378 364" 
			mins "-20 -20 -92" 
			maxs "20 20 92" 
		}

		// Ragnar: Display some text for anyone who got into the pillars. 
		create 
		{ 
			targetname "pillar_message_print" 
			scriptname "pillar_message_print" 
			message "^1WARNING: ^3ILLEGAL PLAYER LOCATION - PILLAR EXPLOIT." 
			origin "-583 810 364" 
			classname "target_print" 
		} 

		// Ragnar: South pillar - hurt 
		create 
		{ 
			scriptname "pillar1_hurt" 
			spawnflags 24 // SLOW+NO_PROTECTION 
			classname "trigger_hurt" 
			dmg 25 
			sound "sound/player/hurt_barbwire.wav" 
			origin "-583 810 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: South pillar - trigger text message 
		create 
		{ 
			scriptname "pillar1_message" 
			classname "trigger_multiple" 
			target "pillar_message_print" 
			origin "-583 810 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: Second pillar from south - hurt 
		create 
		{ 
			scriptname "pillar2_hurt" 
			spawnflags 24 // SLOW+NO_PROTECTION 
			classname "trigger_hurt" 
			dmg 25 
			sound "sound/player/hurt_barbwire.wav" 
			origin "-583 1002 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: Second pillar from south - trigger text message 
		create 
		{ 
			scriptname "pillar2_message" 
			classname "trigger_multiple" 
			target "pillar_message_print" 
			origin "-583 1002 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: Second pillar from north - hurt 
		create 
		{ 
			scriptname "pillar3_hurt" 
			spawnflags 24 // SLOW+NO_PROTECTION 
			classname "trigger_hurt" 
			dmg 25 
			sound "sound/player/hurt_barbwire.wav" 
			origin "-583 1186 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: Second pillar from north - trigger text message 
		create 
		{ 
			scriptname "pillar3_message" 
			classname "trigger_multiple" 
			target "pillar_message_print" 
			origin "-583 1186 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: North pillar - hurt 
		create 
		{ 
			scriptname "pillar4_hurt" 
			spawnflags 24 // SLOW+NO_PROTECTION 
			classname "trigger_hurt" 
			dmg 25 
			sound "sound/player/hurt_barbwire.wav" 
			origin "-583 1378 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		} 

		// Ragnar: North pillar - trigger text message 
		create 
		{ 
			scriptname "pillar4_message" 
			classname "trigger_multiple" 
			target "pillar_message_print" 
			origin "-583 1378 364" 
			mins "-4 -4 -92" 
			maxs "4 4 92" 
		}

		//phisherman: anti-walljump-teamdoor

		create
		{
			model "*41"
			classname "func_door_rotating"
			scriptname "hqdoor2"
			targetname "hqdoor2"
			origin "-33 5567 572"
			type "5"
			allowteams "allies,cvops"
		}

		remapshader "models/mapobjects/tanks_sd/jagdpanther_full"		"textures/caen_4/tank1"
		remapshader "models/mapobjects/tanks_sd/jagdpanther_additions_desert"	"textures/caen_4/tank2"
		remapshader "models/mapobjects/tanks_sd/mg42turret" 			"textures/caen_4/tank3"
		remapshader "models/mapobjects/blitz_sd/blitz_sd_body"			"textures/caen_4/blitz_body"
		remapshader "models/mapobjects/blitz_sd/blitz_sd_windows"		"textures/caen_4/blitz_window"
		remapshaderflush
		
		// Game rules
		wm_axis_respawntime	12
		wm_allied_respawntime	20
		wm_number_of_objectives 8
		wm_set_round_timelimit	25

		// Objectives
		// 1: Capture Town
                // 2: fix Tank
		// 3: Cross River
		// 4: Blow hq wall
		// 5: Steal maps
		// 6: Escape with maps
		// 7: Allied command post
		// 8: Axis command post

		// Current main objectives for each team (0=Axis, 1=Allies)
		wm_set_main_objective		1	0
		wm_set_main_objective		1	1

		wm_objective_status 1 0 0
		wm_objective_status 1 1 0
		wm_objective_status 2 0 0
		wm_objective_status 2 1 0
		wm_objective_status 3 0 0
		wm_objective_status 3 1 0
		wm_objective_status 4 0 0
		wm_objective_status 4 1 0
		wm_objective_status 5 0 0
		wm_objective_status 5 1 0
		wm_objective_status 6 0 0
		wm_objective_status 6 1 0
		wm_objective_status 7 0 0
		wm_objective_status 7 1 0
                wm_objective_status 8 0 0
		wm_objective_status 8 1 0

		wm_set_main_objective		1	0
		wm_set_main_objective		1	1

		// Stopwatch mode defending team (0=Axis, 1=Allies)
		wm_set_defending_team	1

		// Winner on expiration of round timer (0=Axis, 1=Allies)
		wm_setwinner	1

		wait 500
		setautospawn	"Axis spawn"	0
		setautospawn	"Town"	1

                accum 1 set 0  // maps stolen?
		accum 4 set 0 // have the Axis stolen the tank?

		// start triggered loops on/off as required (eg: command post radio sounds)
		disablespeaker allies_compost_sound
		disablespeaker axis_compost_sound
		disablespeaker allies_compost_sound_lms
		disablespeaker axis_compost_sound_lms

		wait 2000

//		wm_announce "^oThis server is running the ^7Caen-Fixpack #1^o!"

		// *----------------------------------- vo ------------------------------------------*
                wm_addteamvoiceannounce 0 "goldrush_allies_tank_steal"
		wm_addteamvoiceannounce 0 "caen2_axis_city_capture"
		wm_addteamvoiceannounce 0 "radar_allies_entrance2_destroy"
		wm_addteamvoiceannounce 0 "axis_hq_compost_construct"

		wm_addteamvoiceannounce 1 "goldrush_axis_tank_steal"
                wm_addteamvoiceannounce 1 "caen2_allies_city_capture"
		wm_addteamvoiceannounce 1 "radar_axis_entrance2_defend"
		wm_addteamvoiceannounce 1 "allies_hq_compost_construct"
		
                wm_teamvoiceannounce 0 "goldrush_allies_tank_steal"
		wm_teamvoiceannounce 0 "caen2_axis_city_capture"
		wm_teamvoiceannounce 0 "radar_allies_entrance2_destroy"
		wm_teamvoiceannounce 0 "axis_hq_compost_construct"

		wm_teamvoiceannounce 1 "goldrush_axis_tank_steal"
                wm_teamvoiceannounce 1 "caen2_allies_city_capture"
		wm_teamvoiceannounce 1 "radar_axis_entrance2_defend"
		wm_teamvoiceannounce 1 "allies_hq_compost_construct"
		


		// *---------------------------------------------------------------------------------*


	}

	

	trigger axis_move_tank
	{
		accum 4 abort_if_not_equal 0
		               
		wm_announce "The Axis are moving the Tank!"
               // *----------------------------------- vo ------------------------------------------*
                
		wm_removeteamvoiceannounce 0 "goldrush_allies_tank_steal"
		wm_removeteamvoiceannounce 1 "goldrush_axis_tank_steal"
		
               	wm_addteamvoiceannounce 1 "axis_hq_tank_stop"
		wm_addteamvoiceannounce 0 "allies_hq_tank_escort"		
		
		wm_teamvoiceannounce 0 "goldrush_allies_tank_stolen"		
		wm_teamvoiceannounce 0 "allies_hq_tank_escort"	
		
		wm_teamvoiceannounce 1 "goldrush_axis_tank_stolen"
		wm_teamvoiceannounce 1 "axis_hq_tank_stop"
		        		
                // *---------------------------------------------------------------------------------*

                
                wm_objective_status 2 0 1
		wm_objective_status 2 1 2
                
                wm_set_main_objective		3	0
		wm_set_main_objective		3	1

                trigger cityflag force_axis // Switch forward spawn to Axis ONLY
		trigger cityflag kill //Remove flag

		accum 4 set 1
		
                
	
        }


        trigger stolen_maps
	{
		accum 1 inc 1				// flag 
		trigger game_manager checkgame		// Check for end of game
	}

        trigger checkgame
	{
		accum 1 abort_if_not_equal 1
		wm_setwinner 0
		wait 400
		wm_endround
	}

}

maps
{
	spawn
	{        
	}

	trigger stolen
	{
		
		wm_objective_status 5 0 1
		wm_objective_status 5 1 2

		wm_set_main_objective 6 0
		wm_set_main_objective 6 1

		

		
	}

	trigger returned
	{
		wm_objective_status 5 0 0
		wm_objective_status 5 1 0

		wm_set_main_objective 5 0
		wm_set_main_objective 5 1

	}

	trigger captured
	{
	}
}
	

cityflag
{
	spawn
	{
		accum 0 set 1	// Who has the flag: 0-Axis, 1-Allied
	}

	trigger axis_capture	// Flag has been touched by an Axis player
	{
		accum 0 abort_if_equal 0 // do Axis own flag?

		accum 0 set 0 // Axis own the pole
		wm_announce	"Axis captured the town!"
                
             // *----------------------------------- vo ------------------------------------------*
                wm_removeteamvoiceannounce 0 "caen2_axis_city_capture"
                wm_removeteamvoiceannounce 1 "caen2_allies_city_capture"
		wm_addteamvoiceannounce 1 "caen2_axis_city_capture"
                wm_addteamvoiceannounce 0 "caen2_allies_city_capture"

		wm_teamvoiceannounce 0 "citycap"
		wm_teamvoiceannounce 0 "goldrush_allies_tank_steal"

		wm_teamvoiceannounce 1 "citylost"
		wm_teamvoiceannounce 1 "goldrush_axis_tank_steal"		
                // *---------------------------------------------------------------------------------*


                wm_objective_status 		1 0 1
		wm_objective_status 		1 1 2
                wm_set_main_objective 2 0
		wm_set_main_objective 2 1
                alertentity city_wobj
		setautospawn	"Allied spawn" 1
		setautospawn	"Town"	0	
		
	}

	trigger allied_capture	// Flag has been touched by an allied player
	{
		accum 0 abort_if_equal 1 // do Allies own flag?

		accum 0 set 1 // Allied own the flag
		wm_announce	"Allies reclaim the town!"
                
              // *----------------------------------- vo ------------------------------------------*
		wm_removeteamvoiceannounce 1 "caen2_axis_city_capture"
                wm_removeteamvoiceannounce 0 "caen2_allies_city_capture"
                wm_addteamvoiceannounce 0 "caen2_axis_city_capture"
                wm_addteamvoiceannounce 1 "caen2_allies_city_capture"
	
		wm_teamvoiceannounce 1 "citycap"
		wm_teamvoiceannounce 0 "citylost"		
                // *---------------------------------------------------------------------------------*

                 
                wm_objective_status 		1 0 2
		wm_objective_status 		1 1 1
                wm_set_main_objective 1 0
		wm_set_main_objective 1 1
		alertentity city_wobj
                setautospawn	"Axis spawn"	0
		setautospawn	"Town"	1

	}

        trigger force_axis
	{
		accum 0 abort_if_equal 0 // Do Axis own the flag?

		alertentity city_wobj
		alertentity cityspawns
	}

	trigger kill
	{
		remove
	}

}

maps_radio // Exit point of map
{
	death
	{
		trigger game_manager stolen_maps
	}
}


// BUILD STUFF==================================================================

houseMG42
{ 
   spawn 
   { 
      wait 200 
      constructible_class 2
      trigger self startup 
       
   } 

   buildstart final 
   { 
   } 

   built final 
   {  
      setstate houseMG42_model default 
      setstate houseMG42_materials invisible 

      // Some kind of UI pop-up to alert players 
      wm_announce   "Allied team has built the house mg42!" 
   } 

   decayed final 
   { 
      trigger self startup 
   } 

   death 
   { 
      trigger self startup 
      // Some kind of UI pop-up to alert players 
      wm_announce   "Axis team has destroyed the house mg42!" 
   } 

   trigger startup 
   {  
      setstate houseMG42_model invisible 
      setstate houseMG42_materials default 
      repairmg42 houseMG42_model
   } 
} 


bridgeMG42 
{ 
   spawn 
   { 
      wait 200 
      constructible_class 2 
      trigger self startup 
   } 

   buildstart final 
   { 
   } 

   built final 
   {  
      setstate bridgeMG42_model default 
      setstate bridgeMG42_materials invisible 

      // Some kind of UI pop-up to alert players 
      wm_announce   "Allied team has built the bridge mg42!" 
   } 

   decayed final 
   { 
      trigger self startup 
   } 

   death 
   { 
      trigger self startup 
      // Some kind of UI pop-up to alert players 
      wm_announce   "Axis team has destroyed the bridge mg42!" 
   } 

   trigger startup 
   {  
      setstate bridgeMG42_model invisible 
      setstate bridgeMG42_materials default 
      repairmg42 bridgeMG42_model
   } 
}

Bridge
{
	spawn
	{
		set
		{
			spawnflags "4"
		}

		wait 400
		constructible_class 3
	}

	buildstart final
	{
		setstate bridge_materials default
		setstate bridge_flag default
	}

	decayed final
	{
		setstate bridge_materials default
		setstate bridge_flag default
	}

	built final
	{
		setstate bridge_materials invisible
		setstate bridge_flag invisible               

		wm_announce "The Bridge has been repaired!"
	}

	death
	{
		setstate bridge_materials default
		setstate bridge_flag default

		wm_announce "The Bridge has been damaged!"
	}
}

gate
{ 
	spawn 
	{ 
		set
		{
			shortname "Gate"
		}

		wait 200 
		constructible_class 3 
		constructible_chargebarreq 1.5 
		setstate gate_materials invisible
	} 

   buildstart final 
   { 
   } 

   built final 
   {  
      setstate gate_materials invisible 

      // Some kind of UI pop-up to alert players 
      wm_announce   "Allied team has rebuilt the gate!" 
	wm_addteamvoiceannounce 0 "radar_allies_entrance2_destroy"
	wm_teamvoiceannounce 0 "radar_allies_entrance2_destroy"
	wm_teamvoiceannounce 1 "radar_axis_entrance2_defend"
	wm_addteamvoiceannounce 1 "radar_axis_entrance2_defend"

   } 

   decayed final 
   { 
      trigger self startup 
   } 

   death 
   { 
      trigger self startup 
      // Some kind of UI pop-up to alert players 
		wm_announce   "Axis team has destroyed the gate!" 
		wm_removeteamvoiceannounce 0 "radar_allies_entrance2_destroy"
		wm_removeteamvoiceannounce 1 "radar_axis_entrance2_defend"
		wm_teamvoiceannounce 0 "radar_allies_entrance2_destroyed"		
                wm_teamvoiceannounce 1 "radar_axis_entrance2_destroyed"

   } 

   trigger startup 
   {  
       
      setstate gate_materials default 
      
   } 
} 

allied_compost_built
{
	spawn
	{
		wait 400
		trigger allied_compost_built setup

		constructible_class 2
	}

	trigger setup
	{
		setchargetimefactor 1 soldier 1
		setchargetimefactor 1 lieutenant 1
		setchargetimefactor 1 medic 1
		setchargetimefactor 1 engineer 1
		setchargetimefactor 1 covertops 1
		sethqstatus 1 0
	}

	buildstart final
	{
		setstate allied_compost_built_model underconstruction
		setstate neutral_compost_closed_clip invisible
		setstate neutral_compost_closed_model invisible
	}

	built final
	{
		setstate allied_compost_built_model default
		setstate neutral_compost_closed_clip invisible
		setstate neutral_compost_closed_model invisible

		trigger allied_compost_built_model enable_allied_features

		enablespeaker allies_compost_sound
	}

	decayed final
	{
		setstate allied_compost_built_model invisible
		setstate neutral_compost_closed_clip default
		setstate neutral_compost_closed_model default
	}

	death
	{
		setstate allied_compost_built_model invisible
		setstate neutral_compost_closed_clip default
		setstate neutral_compost_closed_model default

		trigger allied_compost_built_model disable_allied_features

		disablespeaker allies_compost_sound
	}
}

allied_compost_built_model
{
	spawn
	{
		wait 400
		setstate allied_compost_built_model invisible
	}

	trigger enable_allied_features
	{
		setchargetimefactor 1 soldier 0.75
		setchargetimefactor 1 lieutenant 0.75
		setchargetimefactor 1 medic 0.75
		setchargetimefactor 1 engineer 0.75
		setchargetimefactor 1 covertops 0.75
		sethqstatus 1 1

		wm_announce	"Allied Command Post constructed. Charge speed increased!"

		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "axis_hq_compost_constructed_allies"
		wm_teamvoiceannounce 1 "allies_hq_compost_constructed"
		wm_removeteamvoiceannounce 1 "allies_hq_compost_construct"
		// *---------------------------------------------------------------------------------*


		wm_objective_status 7 0 2
		wm_objective_status 7 1 1
	}

	trigger disable_allied_features
	{
		setchargetimefactor 1 soldier 1
		setchargetimefactor 1 lieutenant 1
		setchargetimefactor 1 medic 1
		setchargetimefactor 1 engineer 1
		setchargetimefactor 1 covertops 1
		sethqstatus 1 0

		wm_announce	"Axis team has destroyed the Allied Command Post!"

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 1 "allies_hq_compost_construct"
	
		wm_teamvoiceannounce 0 "axis_hq_compost_construct"
		wm_teamvoiceannounce 1 "allies_hq_compost_damaged"
		wm_teamvoiceannounce 1 "allies_hq_compost_construct"
		// *---------------------------------------------------------------------------------*

		wm_objective_status 7 0 0
		wm_objective_status 7 1 0
	}
}

axis_compost_built
{
	spawn
	{
		wait 400
		trigger axis_compost_built setup

		constructible_class 2
	}

	trigger setup
	{
		setchargetimefactor 0 soldier 1
		setchargetimefactor 0 lieutenant 1
		setchargetimefactor 0 medic 1
		setchargetimefactor 0 engineer 1
		setchargetimefactor 0 covertops 1
		sethqstatus 0 0
	}

	buildstart final
	{
		setstate axis_compost_built_model underconstruction
		setstate neutral_compost_closed_clip invisible
		setstate neutral_compost_closed_model invisible
	}

	built final
	{
		setstate axis_compost_built_model default
		setstate neutral_compost_closed_clip invisible
		setstate neutral_compost_closed_model invisible

		trigger axis_compost_built_model enable_axis_features

		enablespeaker axis_compost_sound
	}

	decayed final
	{
		setstate axis_compost_built_model invisible
		setstate neutral_compost_closed_clip default
		setstate neutral_compost_closed_model default
	}

	death
	{
		setstate axis_compost_built_model invisible
		setstate neutral_compost_closed_clip default
		setstate neutral_compost_closed_model default

		trigger axis_compost_built_model disable_axis_features

		disablespeaker axis_compost_sound
	}
}

axis_compost_built_model
{
	spawn
	{
		wait 400
		setstate axis_compost_built_model invisible
	}

	trigger enable_axis_features
	{
		setchargetimefactor 0 soldier 0.75
		setchargetimefactor 0 lieutenant 0.75
		setchargetimefactor 0 medic 0.75
		setchargetimefactor 0 engineer 0.75
		setchargetimefactor 0 covertops 0.75
		sethqstatus 0 1

		wm_announce	"Axis Command Post constructed. Charge speed increased!"

		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "axis_hq_compost_constructed"
		wm_teamvoiceannounce 1 "allies_hq_compost_constructed_axis"
		wm_removeteamvoiceannounce 0 "axis_hq_compost_construct"
		// *---------------------------------------------------------------------------------*


		wm_objective_status 8 0 1
		wm_objective_status 8 1 2
	}

	trigger disable_axis_features
	{
		setchargetimefactor 0 soldier 1
		setchargetimefactor 0 lieutenant 1
		setchargetimefactor 0 medic 1
		setchargetimefactor 0 engineer 1
		setchargetimefactor 0 covertops 1
		sethqstatus 0 0

		wm_announce	"Allied team has destroyed the Axis Command Post!"

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 0 "axis_hq_compost_construct"
		wm_teamvoiceannounce 0 "axis_hq_compost_damaged"
		wm_teamvoiceannounce 0 "axis_hq_compost_construct"

		wm_teamvoiceannounce 1 "allies_hq_compost_construct"
		// *---------------------------------------------------------------------------------*

		wm_objective_status 8 0 0
		wm_objective_status 8 1 0
	}
}



// TANK STUFF==================================================================


tank_sound
{
	trigger start
	{
		trigger tank sound_start
		wait 3400
		trigger tank sound_move
	}

	trigger stop
	{
		trigger tank sound_stop
		wait 1400
		trigger tank sound_idle
	}

	trigger rebirth
	{
		trigger tank sound_rebirth
		wait 1400
		trigger tank sound_idle
	}
}

// digibob: converting truck script from goldrush over...
// ============================================================================
// accum 0, track state
// accum 1
//  - bit 0: bridge state		( 0 = not built,	1 = built		)
//  - bit 1: barrier state		( 0 = not built,	1 = built		)
//  - bit 2: spline status 		( 0 = not moving, 	1 = moving 		)
//  - bit 3: stuck check flag 	( 0 = not stuck, 	1 = stuck 		)
//  - bit 4: flag for message	( 0 = dont display, 1= display		)
//  - bit 5: blank				(									)
//  - bit 6: temp register 		(XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
//  - bit 7: death status 		( 0 = alive, 		1 = dead		)
//  - bit 8: player check 		( 0 = players, 		1 = no players	)
//  - bit 9: visible state		( 0 = alive, 		1 = dead		)
// accum 2, blank
// accum 3, current movement loop position
// accum 4, stop counter
// accum 5, lockout ref counter
// accum 6, blank
// accum 7, blank
// ===========================================================================================
// spline points are spln2 -> spln47
// ===========================================================================================
// track events:
//  - tracks_forward
//  - tracks_stop
//  - tracks_turn_left
//  - tracks_turn_right


tank
{
	spawn
	{
		wait 400
		followspline 0 spln1 50000 length 32 wait

		trigger tank sound_idle
		trigger tank tracks_stop
	}

	// ========================================
	// sound stuff

	trigger sound_idle
	{
		stopsound
		playsound sound/vehicles/tank/tank_idle.wav looping volume 512
	}

	trigger sound_start
	{
		stopsound
		playsound sound/vehicles/tank/tank_revup.wav volume 196
	}

	trigger sound_move
	{
		stopsound
		playsound sound/vehicles/tank/tank_move.wav looping volume 512
	}

	trigger sound_stop
	{
		stopsound
		playsound sound/vehicles/tank/tank_revdown.wav volume 196
	}

	trigger sound_death
	{
		stopsound
		playsound sound/vehicles/tank/tank_stop.wav volume 256
	}

	trigger sound_rebirth
	{
		stopsound
		playsound sound/vehicles/tank/tank_start.wav volume 196
	}


		
	

	// ========================================
	// ========================================


// ===========================================================================================
// ===========================================================================================
	trigger tracks_forward
	{
		accum 0 abort_if_equal 1
		accum 0 set 1

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_alt_forward

		remapshaderflush
	}

	trigger tracks_stop
	{
		accum 0 abort_if_equal 0
		accum 0 set 0

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_left
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_right
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_left
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_right
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_left
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_right

		remapshaderflush
	}

	trigger tracks_turn_left
	{
		accum 0 abort_if_equal 2
		accum 0 set 2

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_alt_forward

		remapshaderflush
	}

	trigger tracks_turn_right
	{
		accum 0 abort_if_equal 3
		accum 0 set 3

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_alt_backward

		remapshaderflush
	}

// ===========================================================================================
// ===========================================================================================

	trigger run_continue
	{
		accum 3 inc 1
		trigger self deathcheck
		trigger self stopcheck
		trigger self move
	}


// ===========================================================================================
// movement

	trigger move_check
	{
		trigger self stuck_check
		accum 1 abort_if_bitset 3
                
                trigger self dispatch
	}

	trigger move
	{
		trigger self move_check

		wait 500

		trigger self move
	}

	trigger dispatch
	{
		accum 3 trigger_if_equal 0 tank run_0
		accum 3 trigger_if_equal 1 tank run_1
		accum 3 trigger_if_equal 2 tank run_2
		accum 3 trigger_if_equal 3 tank run_3
		accum 3 trigger_if_equal 4 tank run_4
		accum 3 trigger_if_equal 5 tank run_5
		accum 3 trigger_if_equal 6 tank run_6
		accum 3 trigger_if_equal 7 tank run_7
		accum 3 trigger_if_equal 8 tank run_8
		accum 3 trigger_if_equal 9 tank run_9
		accum 3 trigger_if_equal 10 tank run_10
		accum 3 trigger_if_equal 11 tank run_11
		accum 3 trigger_if_equal 12 tank run_12
		accum 3 trigger_if_equal 13 tank run_13
		accum 3 trigger_if_equal 14 tank run_14
		accum 3 trigger_if_equal 15 tank run_15
			
        }

	trigger run_0
	{
		trigger self tracks_forward
		accum 1 bitset 2
		followspline 0 spln2 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_1
	{
		trigger self tracks_forward
		trigger game_manager axis_move_tank

		accum 1 bitset 2
		followspline 0 spln3 40 wait length 32
		accum 1 bitreset 2

		wm_objective_status 2 0 1
		wm_objective_status 2 1 2
                
                wm_set_main_objective		3	0
		wm_set_main_objective		3	1


		trigger self run_continue
	}

	trigger run_2
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln4 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_3
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln5 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_4
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln6 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_5
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln7 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_6
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln8 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_7
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln9 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_8
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln10 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_9
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln11 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_10
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln12 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_11
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln13 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_12
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln14 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_13
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln15 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_14
	{
		trigger self tracks_forward

		accum 1 bitset 2
		followspline 0 spln16 40 wait length 32
		accum 1 bitreset 2

		trigger self run_continue
	}

	trigger run_15
	{
		trigger self tracks_stop

		accum 1 bitset 2
		followspline 0 spln17 40 wait length 32
		accum 1 bitreset 2

		trigger self tracks_stop
		trigger tank sound_idle
 		wm_objective_status 		3 0 1
		wm_objective_status 		3 1 2
                wm_set_main_objective 4 0
		wm_set_main_objective 4 1

	
                accum 3 inc 1
		wait 300		
		wm_removeteamvoiceannounce 1 "axis_hq_tank_stop"
		wm_removeteamvoiceannounce 0 "allies_hq_tank_escort"

		wm_addteamvoiceannounce 0 "steal"
		wm_addteamvoiceannounce 1 "protect"
		wm_teamvoiceannounce 0 "explosion"
		wm_teamvoiceannounce 0 "steal"

		wm_teamvoiceannounce 1 "explosion"
		wm_teamvoiceannounce 1 "protect"
		wait 200
		trigger antenna crash
		wait 5000
		wm_announce "^3The allies HQ's antenna was destroyed by an airstrike."
		wm_announce "The Allies can now dynamite the Weak Wall!"

		setstate wall_toi default
		setstate hqdoor2 invisible
	}


// ===========================================================================================
// ===========================================================================================

// ===========================================================================================
// stuck checking
// digibob: since we cant return variables, just use bit 3 of accum 1 as a register... mmmm assembly...
	trigger stuck_check
	{
		accum 1 bitreset 3

		trigger self stuck_check_bridge_built
		trigger self stuck_check_scriptlockout
		trigger self stuck_check_finished
	}

	trigger stuck_check_finished
	{
		accum 3 abort_if_less_than 16

		accum 1 bitset 3
	}

	trigger stuck_check_scriptlockout
	{
		accum 5 abort_if_equal 0

		accum 1 bitset 3
	}

	trigger stuck_check_bridge_built
	{
		accum 3 abort_if_not_equal 17

		accum 1 abort_if_not_bitset 0

		accum 1 bitset 3
	}

	trigger stuck_check_bridge_built_msg
	{
		accum 1 abort_if_not_bitset 0
                
		
	}

	

        

// ===========================================================================================
// stop check

	trigger stopcheck_setup
	{
		accum 1 bitset 6			// stop if we're stuck/no-one's pushing :)

		accum 1 abort_if_bitset 8		// no one in the trigger, abort

		trigger self stuck_check		// call the stop check function
		accum 1 abort_if_bitset 3		// we're stuck so break out

		accum 1 bitreset 6			// we're free to move
	}

	trigger stopcheck
	{
		trigger self stopcheck_setup
		accum 1 abort_if_not_bitset 6

		trigger self script_lockout

		// Any just stopped moving stuff goes here
		trigger tank_sound 	stop
		trigger self tracks_stop
		startanimation 45 	10 	15 nolerp norandom
		wait 666
		startanimation 0 	1 	15 nolerp norandom
		wait 900
                
		trigger self script_lockout_stop
		
	}

// ===========================================================================================
// ===========================================================================================
// script lockouts

	trigger script_lockout
	{
		accum 5 inc 1
	}

	trigger script_lockout_stop
	{
		accum 5 inc -1
	}

// ===========================================================================================
// ===========================================================================================
// enable/disable

	trigger tank_enable
	{
		trigger self stuck_check
		accum 1 abort_if_bitset 3 	// stuck check

		accum 4 set 0				// reset stop counter
		accum 1 bitreset 8			// reset stop check

		accum 1 abort_if_bitset 2 	// already following spline
		accum 5 abort_if_not_equal 0	// are we not in a script lockout?

		accum 1 abort_if_bitset 7 	// death check

		// Any just started moving stuff goes here

		trigger self script_lockout
                trigger tank_sound start
		

		startanimation 55 10 15 nolerp norandom
		wait 666
		startanimation 5 40 15 nolerp norandom
		wait 500
		trigger self tracks_forward
                       
		trigger self script_lockout_stop
                
		trigger self move
	}

	trigger tank_disable
	{
		accum 4 inc 1				// up the stop counter
		accum 4 abort_if_less_than 4

		accum 1 bitset 8			// set stop check

		trigger self deathcheck
	}

// ===========================================================================================
// ===========================================================================================
// death / rebirth

	rebirth
	{
		accum 1 bitreset 9 // we're visibly alive
		accum 1 bitreset 7 // we're alive again

		trigger self script_lockout

		changemodel models/mapobjects/tanks_sd/jagdpanther_africa_tracks.md3

		setstate tank_smoke invisible
               // *----------------------------------- vo ------------------------------------------*
		wm_removeteamvoiceannounce 0 "allies_hq_tank_damaged_axis"
                wm_teamvoiceannounce 0 "allies_hq_tank_repaired"

                wm_teamvoiceannounce 1 "axis_hq_tank_repaired_allies"
		wm_teamvoiceannounce 1 "axis_hq_tank_stop"

		
		// *---------------------------------------------------------------------------------*


		trigger tank_sound rebirth
		wait 500
                

		trigger self script_lockout_stop
                resetscript
	}

	death
	{
		accum 1 bitset 7
	}

	trigger deathcheck
	{
		accum 1 abort_if_not_bitset 7	// are we dead?
		accum 1 abort_if_bitset 9		// are we not already visibly dead?
		accum 1 abort_if_bitset 2		// are we not following a spline?
		accum 5 abort_if_not_equal 0	// are we not in a script lockout?

		accum 1 bitset 9				// we're now visibly dead

		trigger self deathcheck_message
		accum 1 bitset 4

		changemodel models/mapobjects/tanks_sd/jagdpanther_africa_tracks2.md3

		setstate tank_smoke default

		kill tank_construct

		trigger self sound_death
		trigger self tracks_stop

		trigger self script_lockout

		trigger self tracks_stop
		startanimation 45 10 15 nolerp norandom
		wait 666
		startanimation 0 1 15 nolerp norandom

		trigger self script_lockout_stop

		resetscript
	}

        

	trigger deathcheck_message
	{
		accum 1 abort_if_not_bitset 4

		wm_announce "The Tank has been damaged!"
             // *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 0 "allies_hq_tank_damaged_axis"
		wm_teamvoiceannounce 0 "allies_hq_tank_damaged_axis"
		wm_teamvoiceannounce 1 "axis_hq_tank_damaged"
		
		// *--------------------------------------------------------------------------------*

		
	}

	trigger startfire
	{
		startanimation 67 8 10 nolerp norandom noloop
	}

	trigger stopfire
	{
		startanimation 0 1 15 nolerp norandom
	}

	trigger enable_stage2
	{
		accum 1 bitreset 0
	}

	trigger disable_stage2
	{
		accum 1 bitset 0
	}

	trigger enable_stage4
	{
		accum 1 bitreset 1
	}

	trigger disable_stage4
	{
		accum 1 bitset 1
	}
}



tank_trigger
{
	spawn
	{
		wait 500

		attachtotag tank_shell tag_turret

		trigger tank_trigger bot_goal_loop
	}

	trigger bot_goal_loop
	{
		wait 4000	// check every 4 seconds
		trigger tank bot_active_check
		trigger tank_trigger bot_goal_loop
	}
}

tank_build
{
	spawn
	{
		wait 500

		attachtotag tank_shell tag_turret
	}
}

tank_construct
{
	spawn
	{
		wait 1000

		kill tank

		constructible_class 3
		constructible_health 1200
		constructible_constructxpbonus 10
		constructible_destructxpbonus 10
	}

	built final
	{
		alertentity tank

		wm_announce "The Tank has been repaired!"
	}
}

tank_shell
{
	spawn
	{
		wait 400
		attachtotag tank tag_tank
	}
}

tank_turret
{
	spawn
	{
		wait 500

		attachtotag tank_shell tag_turret
	}

	trigger turn
	{
		wait 1000

		playsound sound/vehicles/tank/turret_spin.wav looping
		faceangles 0 340 0 3000
		stopsound
		playsound sound/vehicles/tank/turret_end.wav

		wait 500

		trigger tank startfire
		playsound sound/vehicles/tank/tank_fire.wav
		trigger boomtrigger boom_all		// triggers the hurts around explosions (each has its own wait for timing)
		trigger tank_flash run
	}

}

antenna
{
        spawn
        {
                wait 200
                faceangles 0 0 0 100
        }

        trigger crash
        {
                playsound sound/world/metalbreak.wav
                faceangles 0 0 -90 2000
                alertentity brick
                playsound sound/maps/expl_med.wav
        }
}

tank_flash
{
	spawn
	{
		setstate tank_flash invisible
	}

	trigger run
	{
		setstate tank_flash default
 		attachtotag tank_turret tag_flash
		faceangles 0 90 0 50

		wait 50

		trigger tank_turret blow_wall
	}
}

tank_disabler
{
	trigger run
	{
		trigger tank tank_disable
	}
}

tank_enabler
{
	trigger run
	{
		trigger tank tank_enable
	}
}

tank_smoke
{
	spawn
	{
		wait 300
		attachtotag tank_shell tag_smoke
		setstate tank_smoke invisible
	}
}



// these cause timed damage from the tank barrel to the wall explosion
boomtrigger
{
	trigger boom_all		// this is called from the tank to trigger all hurts
	{
		trigger hurt_barrelblast1 boom
		trigger hurt_barrelblast2 boom
		trigger hurt_wallblast boom
		wait 200                
	}
}


hurt_barrelblast1
{
	spawn
	{
		wait 200
		setstate hurt_barrelblast1 invisible
	}

	trigger boom
	{
		setstate hurt_barrelblast1 default
		wait 100
		remove
	}
}

hurt_barrelblast2
{
	spawn
	{
		wait 200
		setstate hurt_barrelblast2 invisible
	}

	trigger boom
	{
		wait 165
		setstate hurt_barrelblast2 default
		wait 100
		remove
	}
}


hurt_wallblast
{
	spawn
	{
		wait 200
		setstate hurt_wallblast invisible

		setstate hurt_barrelblast2 invisible
	}

	trigger boom
	{
		wait 200
		setstate hurt_wallblast default
		wait 100
		remove
	}
}

wall_toi
{
	spawn
	{
		set
		{
			target "wall"
			track "the Weak Wall"
			spawnflags "2"
		}
	}
}

wall
{
	spawn
	{
		wait 200
		constructible_class 3
		setstate wall_toi invisible
	}

	death
	{
		wm_announce "The Allies have destroyed the Weak Wall!"
		wm_teamvoiceannounce 0 "axis_hq_objective_destroyed"
		wm_teamvoiceannounce 1 "allies_hq_objective_destroyed"
		setstate hqdoor invisible
	}
}

housemg42_toi
{
	spawn
	{
		set
		{
			shortname "House MG42"
		}
	}
}

bridgemg42_toi
{
	spawn
	{
		set
		{
			shortname "Bridge MG42"
		}
	}
}