// et_mor_pro.script
// Changes by: eTTp`Igloo
// version: final (a1)

game_manager
{
	spawn
	{
		remapshader "models/mapobjects/tanks_sd/mg42turret" "models/mapobjects/tanks_sd/mg42turret_2"

		remapshaderflush

		wm_set_round_timelimit	30

		wm_axis_respawntime	30
		wm_allied_respawntime	20

		wm_number_of_objectives 6

		wm_set_defending_team	0
		wm_setwinner	0

		wait 150

		setautospawn	"Gate House"	0
		setautospawn	"Desert Camp"	1

		// objectives
		wm_objective_status 1 0 0 // get the tank over the bridge
		wm_objective_status 1 1 0
		wm_objective_status 2 0 0 // the tank shoots the gate
		wm_objective_status 2 1 0
		wm_objective_status 3 0 0 // blow up the east gate
		wm_objective_status 3 1 0
		wm_objective_status 4 0 0 // use the keycard
		wm_objective_status 4 1 0
		wm_objective_status 5 0 0 // get into the bank
		wm_objective_status 5 1 0
		wm_objective_status 6 0 0 // escape with the gold
		wm_objective_status 6 1 0

		wait 150
		alertentity case1
		setstate case1 invisible

		//Igloo - additional possibilities:

		//alertentity possible_window_1

		//alertentity axis_spawn_wall

		//alertentity bank_wood_1
		//alertentity bank_wood_2
		//alertentity bank_wood_3
	}

	trigger stolen_case
	{
		wm_announce "^5The Allies have escaped with the Nazi gold!"

		wm_setwinner 1
		wait 3000
		wm_endround
	}
}

// +Spawnkilling protections+

spawnkilling
{
	spawn
	{
		//wait 50

		//protections against spawnkilling by airstrike/artillery

		alertentity nosk_allied_roof_1
	}
}

// Igloo: old house spawn

oldhouse_allies_main
{
	spawn
	{
		wait 50
		setstate oldhouse_allies_main invisible
		setstate oldhouse_allies invisible
	}
}

// Igloo: courtyard spawn

courtyardspawn
{
	spawn
	{
		wait 50
		setstate courtyardred invisible
		setstate courtyardspawn invisible
	}

	trigger enable
	{
		setstate courtyardred default
		setstate courtyardspawn default
	}

	trigger disable
	{
		setstate courtyardred invisible
		setstate courtyardspawn invisible
	}
}

// Igloo: kitchen spawn

kitchen_wolf
{
	spawn
	{
		wait 50
		setstate kitchen_allies invisible
		setstate kitchen_wolf invisible
	}

	trigger enable
	{
		setstate kitchen_allies default
		setstate kitchen_wolf default
		wm_announce "^4Allied Kitchen Spawn enabled (6 positions available)!"
	}

	trigger disable
	{
		setstate kitchen_allies invisible
		setstate kitchen_wolf invisible
		wm_announce "^4Allied Kitchen Spawn is now disabled!"
	}
}

// Igloo: mortars

mortar_fire2
{
	spawn
	{
		wait 50
		faceangles 90 0 0 10

		setstate mortar_fire2 invisible
	}

	trigger start
	{
		setstate mortar_fire2 default
		trigger self drop
	}

	trigger drop
	{
		gotomarker mortar_marker2 1800 wait
		alertentity mortar_boom2

		setstate mortar_fire2 invisible

		alertentity bank_roof_after
		alertentity bank_roof
	}
}

mortar_fire3
{
	spawn
	{
		wait 50
		faceangles 90 0 0 10

		setstate mortar_fire3 invisible
	}

	trigger start
	{
		setstate mortar_fire3 default
		trigger self drop
	}

	trigger drop
	{
		gotomarker mortar_marker3 1800 wait
		alertentity mortar_boom3

		setstate mortar_fire3 invisible

		alertentity bank_effects_front
		alertentity bank_effects_side_b
	}
}

mortar_fire4
{
	spawn
	{
		wait 50
		faceangles 90 0 0 10

		setstate mortar_fire4 invisible
	}

	trigger start
	{
		setstate mortar_fire4 default
		trigger self drop
	}

	trigger drop
	{
		gotomarker mortar_marker4 1800 wait
		alertentity mortar_boom4

		setstate mortar_fire4 invisible

		alertentity bank_effects_side_a
	}
}

// =========================Tank stuff====================================================

tank_sound
{
	trigger start
	{
		trigger tank sound_start
		wait 3400
		trigger tank sound_move
	}

	trigger stop
	{
		trigger tank sound_stop
		wait 1400
		trigger tank sound_idle
	}

	trigger rebirth
	{
		trigger tank sound_rebirth
		wait 1400
		trigger tank sound_idle
	}
}

// accum 1 :
//	- bit 1, spline status
//	- bit 3, stuck check
//	- bit 5, tank end move
//	- bit 6, temp counter
//	- bit 7, death status
//	- bit 8, player check
//	- bit 9, visable state
// accum 3 ; movement loop position
// accum 4 ; stop counter
// accum 5 ; lockout ref counter
// accum 6 : dynamite on bridge?

tank
{
	spawn
	{
		wait 400
		followspline 0 sp0 5000 length 32 wait

		trigger self sound_idle
	}

	trigger sound_idle
	{
		stopsound
		playsound sound/vehicles/tank/tank_idle.wav looping volume 512
	}

	trigger sound_start
	{
		stopsound
		playsound sound/vehicles/tank/tank_revup.wav volume 196
	}

	trigger sound_move
	{
		stopsound
		playsound sound/vehicles/tank/tank_move.wav looping volume 512
	}

	trigger sound_stop
	{
		stopsound
		playsound sound/vehicles/tank/tank_revdown.wav volume 196
	}

	trigger sound_death
	{
		stopsound
		playsound sound/vehicles/tank/tank_stop.wav volume 256
	}

	trigger sound_rebirth
	{
		stopsound
		playsound sound/vehicles/tank/tank_start.wav volume 196
	}

	trigger tracks_forward
	{
		accum 0 abort_if_equal 1
		accum 0 set 1

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_alt_forward

		remapshaderflush
	}

	trigger tracks_stop
	{
		accum 0 abort_if_equal 0
		accum 0 set 0

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_left
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_right
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_left
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_right
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_left
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_right

		remapshaderflush
	}

	trigger tracks_turn_left
	{
		accum 0 abort_if_equal 2
		accum 0 set 2

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_alt_forward

		remapshaderflush
	}

	trigger tracks_turn_right
	{
		accum 0 abort_if_equal 3
		accum 0 set 3

		remapshader models/mapobjects/tanks_sd/jag_tracks_left 	models/mapobjects/tanks_sd/jag_tracks_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_tracks_right models/mapobjects/tanks_sd/jag_tracks_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_wheels_left 	models/mapobjects/tanks_sd/jag_wheels_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_wheels_right models/mapobjects/tanks_sd/jag_wheels_alt_backward
		remapshader models/mapobjects/tanks_sd/jag_cogs_left 	models/mapobjects/tanks_sd/jag_cogs_alt_forward
		remapshader models/mapobjects/tanks_sd/jag_cogs_right 	models/mapobjects/tanks_sd/jag_cogs_alt_backward

		remapshaderflush
	}

	trigger run_continue
	{
		accum 3 inc 1
		trigger self deathcheck
		trigger self stopcheck
		trigger self move
	}

	trigger move_check
	{
		trigger self stuck_check
		accum 1 abort_if_bitset 3

		trigger self dispatch
	}

	trigger move
	{
		trigger self move_check

		wait 500

		trigger self move
	}

	trigger dispatch
	{
		accum 3 trigger_if_equal 0 tank run_0
		accum 3 trigger_if_equal 1 tank run_1
		accum 3 trigger_if_equal 2 tank run_2
		accum 3 trigger_if_equal 3 tank run_3
		accum 3 trigger_if_equal 4 tank run_4
		accum 3 trigger_if_equal 5 tank run_5
		accum 3 trigger_if_equal 6 tank run_6
		accum 3 trigger_if_equal 7 tank run_7
		accum 3 trigger_if_equal 8 tank run_8
		accum 3 trigger_if_equal 9 tank run_9
		accum 3 trigger_if_equal 10 tank run_10
		accum 3 trigger_if_equal 11 tank run_11
		accum 3 trigger_if_equal 12 tank run_12
		accum 3 trigger_if_equal 13 tank run_13
		accum 3 trigger_if_equal 14 tank run_14
		accum 3 trigger_if_equal 15 tank run_15
		accum 3 trigger_if_equal 16 tank run_16
		accum 3 trigger_if_equal 17 tank run_17
		accum 3 trigger_if_equal 18 tank run_18
		accum 3 trigger_if_equal 19 tank run_19
		accum 3 trigger_if_equal 20 tank run_20
		accum 3 trigger_if_equal 21 tank run_21
		accum 3 trigger_if_equal 22 tank run_22
		accum 3 trigger_if_equal 23 tank run_23
		accum 3 trigger_if_equal 24 tank run_24
		accum 3 trigger_if_equal 25 tank run_25
		accum 3 trigger_if_equal 26 tank run_26
		accum 3 trigger_if_equal 27 tank run_27
		accum 3 trigger_if_equal 28 tank run_28
		accum 3 trigger_if_equal 29 tank run_29
		accum 3 trigger_if_equal 30 tank run_30
	}

	trigger run_0
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp1 50 wait length 32
		followspline 0 sp2 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_1
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp3 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_2
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp4 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_3
	{
		trigger self tracks_turn_left
		accum 1 bitset 1
		followspline 0 sp5 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_4
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp6 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_5
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp7 100 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_6
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp8 100 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_7
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp9 125 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_8
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp10 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_9
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp11 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_10
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp12 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_11
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp13 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_12
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp14 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_13
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp15 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_14
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp16 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_15
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp17 135 wait length 32
		accum 1 bitreset 1
		trigger self run_continue
	}

	trigger run_16
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp18 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_17
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp19 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_18
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp20 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_19
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp21 135 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_20
	{
		setstate oldhouse_allies_main default
		setstate oldhouse_allies default

		wm_announce "^4Allied Old House Spawn enabled (5 positions available)!"

		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp22 100 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	// ====the tank drives over the bridge====
	trigger run_21
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp23 75 wait length 32
		trigger self tracks_turn_right
		followspline 0 sp24 75 wait length 32
		trigger self tracks_forward
		followspline 0 sp25 75 wait length 32
		followspline 0 sp26 75 wait length 32
		followspline 0 sp27 100 wait length 32
		followspline 0 sp29 175 wait length 32
		accum 1 bitreset 1

		wm_objective_status 1 0 2
		wm_objective_status 1 1 1

		trigger self run_continue
	}

	// ==== tank shoots church ====
	trigger run_22
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp30 100 wait length 32
		accum 1 bitreset 1

		trigger self script_lockout

		trigger self tracks_stop
		trigger tank_sound stop

		accum 3 inc 1

		trigger tank_turret shoot1
	}

	trigger run_23
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp31 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_24
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp32 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_25
	{
		trigger self tracks_turn_right
		accum 1 bitset 1
		followspline 0 sp33 75 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_26
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp34 100 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_27
	{
		trigger self tracks_turn_left
		accum 1 bitset 1
		followspline 0 sp35 125 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_28
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp36 125 wait length 32
		accum 1 bitreset 1

		trigger self run_continue
	}

	trigger run_29
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp37 100 wait length 32
		accum 1 bitreset 1

		wm_announce "^8The Tank ^ais nearly at ^8the Main town gate!"

		trigger self run_continue
	}

	// ==== tank stops and shoots gate ====
	trigger run_30
	{
		trigger self tracks_forward
		accum 1 bitset 1
		followspline 0 sp38 75 wait length 32
		accum 1 bitreset 1

		trigger self script_lockout

		trigger self tracks_stop
		trigger tank_sound stop

		trigger tank_turret shoot2

		accum 3 inc 1

		setdamagable tank 0
		settankammo tank 0

		allowtankenter no
		allowtankexit yes
	}

	trigger stuck_check
	{
		accum 1 bitreset 3

		trigger self stuck_check_bridge_built
		trigger self stuck_check_dyn
		trigger self stuck_check_scriptlockout
		trigger self stuck_check_finished
	}

	trigger stuck_check_finished
	{
		accum 3 abort_if_less_than 31

		accum 1 bitset 3
	}

	trigger stuck_check_scriptlockout
	{
		accum 5 abort_if_equal 0

		accum 1 bitset 3
	}

	trigger stuck_check_bridge_built
	{
		accum 3 abort_if_not_equal 21

		accum 1 abort_if_bitset 0

		accum 1 bitset 3
	}

	trigger stuck_check_bridge_dyn
	{
		accum 3 abort_if_not_equal 21

		accum 6 set_to_dynamitecount bridge1
		accum 6 abort_if_equal 0

		accum 1 bitset 3
	}

	trigger tank_bridge
	{
		accum 1 bitset 0
	}

	trigger tank_bridge_dead
	{
		accum 1 bitreset 0
	}

	trigger stopcheck_setup
	{
		accum 1 bitset 6

		accum 1 abort_if_bitset 8

		trigger self stuck_check
		accum 1 abort_if_bitset 3

		accum 1 bitreset 6
	}

	trigger stopcheck
	{
		trigger self stopcheck_setup
		accum 1 abort_if_not_bitset 6

		trigger self script_lockout

		trigger tank_sound stop
		trigger self tracks_stop
		wait 500

		trigger self script_lockout_stop
		resetscript
	}

	trigger script_lockout
	{
		accum 5 inc 1
	}

	trigger script_lockout_stop
	{
		accum 5 inc -1
	}

	trigger tank_enable
	{
		trigger self stuck_check
		accum 1 abort_if_bitset 3

		accum 4 set 0
		accum 1 bitreset 8

		accum 1 abort_if_bitset 1
		accum 5 abort_if_not_equal 0

		accum 1 abort_if_bitset 7

		trigger self script_lockout

		trigger tank_sound start
		wait 500

		trigger self script_lockout_stop

		trigger self move
	}

	trigger tank_disable
	{
		accum 4 inc 1
		accum 4 abort_if_less_than 2

		accum 1 bitset 8

		trigger self deathcheck
	}

	rebirth
	{
		accum 1 bitreset 9
		accum 1 bitreset 7

		trigger self script_lockout

		changemodel models/mapobjects/tanks_sd/jagdpanther_africa_tracks.md3

		setstate tank_smoke invisible

		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "axis_hq_tank_repaired_allies"

		wm_teamvoiceannounce 1 "allies_hq_tank_repaired"
		// *---------------------------------------------------------------------------------*

		trigger tank_sound rebirth

		wait 500

		trigger self script_lockout_stop
	}

	death
	{
		accum 1 bitset 7
	}

	trigger deathcheck
	{
		accum 1 abort_if_not_bitset 7
		accum 1 abort_if_bitset 9
		accum 1 abort_if_bitset 1
		accum 5 abort_if_not_equal 0

		accum 1 bitset 9

		trigger self deathcheck_message
		accum 1 bitset 4

		changemodel models/mapobjects/tanks_sd/jagdpanther_africa_tracks2.md3

		setstate tank_smoke default

		kill tank_construct

		trigger self sound_death
		trigger self tracks_stop

		resetscript
	}

	trigger deathcheck_message
	{
		accum 1 abort_if_not_bitset 4

		wm_announce "^8The Tank ^ahas been damaged^8!"

		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "axis_hq_tank_damaged"

		wm_teamvoiceannounce 1 "allies_hq_tank_damaged_axis"
		// *---------------------------------------------------------------------------------*
	}

	trigger startfire
	{
		startanimation 67 8 10 nolerp norandom noloop
	}

	trigger stopfire
	{
		startanimation 0 1 15 nolerp norandom
	}
}

tank_construct
{
	spawn
	{
		wait 1000

		kill tank

		constructible_class 2
		constructible_health 1200
		constructible_constructxpbonus 10
		constructible_destructxpbonus 10
	}

	built final
	{
		alertentity tank

		wm_announce "^8The Tank ^ahas been repaired^8!"
	}
}

tank_disabler
{
	trigger run
	{
		trigger tank tank_disable
	}
}

tank_enabler
{
	trigger run
	{
		trigger tank tank_enable
	}
}

tank_trigger
{
	spawn
	{
		wait 500

		attachtotag tank_shell tag_turret
	}
}

tank_build
{
	spawn
	{
		wait 500

		attachtotag tank_shell tag_turret
	}
}

tank_turret
{
	spawn
	{
		wait 500

		attachtotag tank_shell tag_turret
	}

	trigger shoot1 //kill church
	{
		wait 800
		playsound sound/vehicles/tank/turret_spin.wav loop
		faceangles 350 5 0 2500
		stopsound
		playsound sound/vehicles/tank/turret_stop.wav

		wait 500

		trigger tank start_fire
		playsound sound/vehicles/tank/turret_stop.wav
		trigger churchblast_hurt boom
		trigger tank_flash shoot1
	}

	trigger shoot2 //kill gate
	{
		wait 800
		playsound sound/vehicles/tank/turret_spin.wav loop
		faceangles 355 5 0 2000
		stopsound
		playsound sound/vehicles/tank/turret_stop.wav

		wait 500

		trigger tank start_fire
		playsound sound/vehicles/tank/turret_stop.wav
		trigger gateblast_hurt boom
		trigger tank_flash shoot2
	}

	trigger blast_church
	{
		setstate tank_flash invisible

		wait 20
		trigger tank stop_fire

		alertentity church_tower
		trigger church_tower_after boom

		wait 100

		playsound sounds/vehicles/tank/turret_spin.wav looping
		faceangles 0 0 0 2000
		stopsound
		playsound sounds/vehicles/tank/turret_end.wav
		trigger tank script_lockout_stop
	}

	trigger blast_gate
	{
		setstate tank_flash invisible

		wait 20
		trigger tank stop_fire

		alertentity gate2
		trigger gate2_after boom

		alertentity gatehouse_axis
		alertentity gatehouse_allied
		alertentity gatehouse_wobj

		//wm_axis_respawntime 25

		setautospawn	"North Market"	0
		setautospawn	"Gate House"	1

		wm_announce "^aAllies have destroyed ^8the Main town gate!"

		wm_objective_status 2 0 2
		wm_objective_status 2 1 1

		trigger axisteamdoor2a close

		// +Spawnkilling protections+
		alertentity nosk_allied_roof_2

		//trigger ldoor_tunnel go_up
		//trigger rdoor_tunnel go_up

		wait 100

		playsound sounds/vehicles/tank/turret_spin.wav looping
		faceangles 0 0 0 2000
		stopsound
		playsound sounds/vehicles/tank/turret_end.wav
		trigger tank sound_death
		trigger tank script_lockout_stop
	}
}

tank_smoke
{
	spawn
	{
		wait 300
		attachtotag tank_shell tag_smoke
		setstate tank_smoke invisible
	}
}

tank_flash
{
	spawn
	{
		setstate tank_flash invisible
	}

	trigger shoot1	// kill church
	{
		setstate tank_flash default
		attachtotag tank_turret tag_flash
		faceangles 350 5 0 50

		wait 20

		trigger tank_turret blast_church
	}

	trigger shoot2	// kill gate
	{
		setstate tank_flash default
		attachtotag tank_turret tag_flash
		faceangles 355 5 0 50

		wait 20

		trigger tank_turret blast_gate
	}
}

tank_shell
{
	spawn
	{
		wait 400
		attachtotag tank tag_tank
	}
}

// =================================End of Tank Stuff============================
//
//
// =================================tank blast stuff=============================

churchblast_hurt
{
	spawn
	{
		wait 200
		setstate churchblast_hurt invisible
	}

	trigger boom
	{
		setstate churchblast_hurt default
		wait 100
		remove
	}
}

church_tower_after
{
	spawn
	{
		wait 100
		setstate church_tower_after invisible
	}

	trigger boom
	{
		setstate church_tower_after default
	}
}

gateblast_hurt
{
	spawn
	{
		wait 200
		setstate gateblast_hurt invisible
	}

	trigger boom
	{
		setstate gateblast_hurt default
		wait 100
		remove
	}
}

gate2_after
{
	spawn
	{
		wait 100
		setstate gate2_after invisible
	}

	trigger boom
	{
		setstate gate2_after default
	}
}

// ===================end of tank blast stuff=====================================

mg1
{
	spawn
	{
		wait 200
		constructible_class 2

		trigger self startup
	}

	built final
	{
		setstate mg1_gun default
		setstate mg1_mat invisible

		wm_announce   "^1Axis team have constructed the North Market's MG nest"
	}

	decayed final
	{
		trigger self startup
	}

	death
	{
		trigger self startup

		wm_announce   "^5Allied team have destroyed the North Market's MG nest"
	}

	trigger startup
	{
		setstate mg1_gun invisible
		setstate mg1_mat default
		repairmg42 mg1_gun
	}
}

gate1
{
	spawn
	{
		wait 200

		constructible_class 2
		constructible_chargebarreq 1.2

		setstate gate1_mat invisible
	}

	built final
	{
		setstate gate1_mat invisible

		wm_announce "^1Axis team have constructed the Villa Courtyard's Gate"
	}

	decayed final
	{
		setstate gate1_mat default
	}

	death
	{
		setstate gate1_mat default

		wm_announce "^5Allied team have destroyed the Villa Courtyard's Gate"
	}
}

axis_compost1_built
{
	spawn
	{
		wait 200
		constructible_class 2
		trigger self setup
		trigger self startup
	}

	buildstart final
	{
		setstate axis_compost1_closed invisible
		setstate axis_compost1_built_model underconstruction
	}

	built final
	{
		setstate axis_compost1_built_model default
		setstate axis_compost1_closed invisible
		setstate axis_compost1_closed_clip invisible

		wm_announce "^1Axis team have constructed a command post. Charge speed increased!"

		trigger self enable
	}

	decayed final
	{
		trigger self startup
	}

	death
	{
		trigger self startup

		trigger self setup

		wm_announce "^5Allied team have destroyed the Axis Commmand Post"
	}

	trigger startup
	{
		setstate axis_compost1_built_model invisible
		setstate axis_compost1_closed default
		setstate axis_compost1_closed_clip default
	}

	trigger setup
	{
		setchargetimefactor 0 soldier 1
		setchargetimefactor 0 lieutenant 1
		setchargetimefactor 0 medic 1
		setchargetimefactor 0 engineer 1
		setchargetimefactor 0 covertops 1
	}

	trigger enable
	{
		setchargetimefactor 0 soldier 0.7
		setchargetimefactor 0 lieutenant 0.8
		setchargetimefactor 0 medic 0.7
		setchargetimefactor 0 engineer 0.7
		setchargetimefactor 0 covertops 0.7
	}
}

bridge1
{
	spawn
	{
		wait 200
		constructible_class 3

		trigger self startup
	}

	built final
	{
		setstate bridge1_mat invisible
		trigger tank tank_bridge

		wm_announce "^5Allied team have constructed the Tank Defences' Bridge"
	}

	decayed final
	{
		trigger self startup
	}

	death
	{
		trigger self startup
		trigger tank tank_bridge_dead

		wm_announce "^1Axis team have destroyed the Tank Defences' Bridge"
	}

	trigger startup
	{
		setstate bridge1_mat default
	}
}

allied_compost1_built
{
	spawn
	{
		wait 200
		constructible_class 2
		trigger self setup
		trigger self startup
	}

	buildstart final
	{
		setstate allied_compost1_closed invisible
		setstate allied_compost1_built_model underconstruction
	}

	built final
	{
		setstate allied_compost1_built_model default
		setstate allied_compost1_closed invisible
		setstate allied_compost1_closed_clip invisible

		wm_announce "^5Allied team have constructed a command post. Charge speed increased!"

		trigger kitchen_wolf enable

		trigger self enable
	}

	decayed final
	{
		trigger self startup
	}

	death
	{
		trigger self startup

		trigger self setup

		wm_announce "^1Axis team have destroyed the Allied Commmand Post!"

		trigger kitchen_wolf disable
	}

	trigger startup
	{
		setstate allied_compost1_built_model invisible
		setstate allied_compost1_closed default
		setstate allied_compost1_closed_clip default
	}

	trigger setup
	{
		setchargetimefactor 1 soldier 1
		setchargetimefactor 1 lieutenant 1
		setchargetimefactor 1 medic 1
		setchargetimefactor 1 engineer 1
		setchargetimefactor 1 covertops 1
	}

	trigger enable
	{
		setchargetimefactor 1 soldier 0.7
		setchargetimefactor 1 lieutenant 0.8
		setchargetimefactor 1 medic 0.7
		setchargetimefactor 1 engineer 0.7
		setchargetimefactor 1 covertops 0.7
	}
}

// Igloo: East Gate (kind of tunnel way to door controls)

east_gate
{
	spawn
	{
		wait 200
		constructible_class 3
	}

	death
	{
		wm_announce "^5Allies have destroyed the East Gate!"

		wm_objective_status 3 0 2
		wm_objective_status 3 1 1
	}
}

// Igloo: safe's actions

safe_push_open
{
	spawn
	{
		setstate safe_push_open default
	}

	activate allies
	{
		globalaccum 7 abort_if_not_equal 1

		trigger safe_push_open allies
	}

	trigger allies
	{
		trigger safe_control open
	}
}

safe_push_close
{
	spawn
	{
		wait 200
		trigger safe_push_close rotate
		setstate safe_push_close invisible
	}

	activate axis
	{
		globalaccum 7 abort_if_not_equal 1

		globalaccum 8 abort_if_not_equal 2
		trigger safe_push_close axis
	}

	trigger axis
	{
		trigger safe_control close
	}

	trigger rotate
	{
		faceangles 0 -120 0 100
	}

	trigger rotate_back
	{
		faceangles 0 0 0 2640
	}
}

safe_main_door
{
	spawn
	{
	}

	trigger rotate
	{
		faceangles 0 -120 0 2640
	}

	trigger rotate_back
	{
		faceangles 0 0 0 2640
	}
}

safe_main_knob
{
	spawn
	{
		setstate safe_main_knob invisible

		wait 400
		trigger safe_main_knob hide
	}

	trigger rotate
	{
		faceangles 0 -120 0 2640
	}

	trigger rotate_back
	{
		faceangles 0 0 0 2640
	}

	trigger hide
	{
		faceangles 0 25 0 1
	}

	trigger unhide
	{
		faceangles 0 0 0 1
	}
}

safe_knob_rot
{
	spawn
	{
	}

	trigger rotate
	{
		setrotation 0 0 100
		wait 2680
		halt
	}

	trigger rotate_close
	{
		setrotation 0 0 -100
		wait 2680
		halt
	}
}

// Igloo: the key card & safe

case2
{
	spawn
	{
		wait 200
		setstate keycard invisible
		setstate keycard_red default
		setstate keycard_target default
	}

	trigger stolen
	{ 
		setstate keycard_target invisible
		setstate keycard_toi invisible
	}


	trigger returned
	{
		setstate keycard_target default
		setstate keycard_toi default
	}

	trigger captured
	{
		wm_announce "^5The Allies have got the access to the Gold Safe!"

		globalaccum 7 set 1	// safe is unlocked

		alertentity sabotage_alert
		trigger doors_keycard active

		wm_objective_status 4 0 2
		wm_objective_status 4 1 1

		setstate keycard_red invisible
		setstate keycard default

		setstate doors_sabotage default

		setstate truck_exitpoint default

		trigger door_key_3a open

		alertentity kitchen_door

		wait 5000

		trigger mortar_fire2 start
		trigger mortar_fire3 start
		trigger mortar_fire4 start
	}

	trigger gate
	{
		trigger ldoor_tunnel go_down
		trigger rdoor_tunnel go_down
	}
}

safe_control
{
	spawn
	{
		globalaccum 7 set 0	//take-check
		accum 1 set 1
	}

	trigger open
	{
		accum 1 abort_if_not_equal 1

		setstate safe_push_open invisible

		alertentity bank_alert

		trigger safe_knob_rot rotate

		wait 2680

		setstate safe_knob_rot invisible

		alertentity safe_main_knob

		trigger safe_main_knob unhide

		wait 50

		trigger safe_main_door rotate
		trigger safe_main_knob rotate

		setstate case1_fake invisible
		setstate case1 default

		wait 2640
		wm_announce "^5Allies have opened the Gold Safe!"

		wait 2000	// + some delay to close

		setstate safe_push_close default
		accum 1 set 0
	}

	trigger close
	{
		accum 1 abort_if_not_equal 0

		setstate safe_push_close invisible

		//setstate safe_main_knob invisible

		alertentity bank_alert

		trigger safe_main_door rotate_back
		trigger safe_main_knob rotate_back

		setstate case1_fake default
		setstate case1 invisible

		wait 2640

		setstate safe_knob_rot default
		trigger safe_main_knob hide
		trigger safe_knob_rot rotate_close

		wait 2680

		wm_announce "^1Axis have closed the Gold Safe!"

		wait 2000	// + some delay to open

		setstate safe_push_open default
		accum 1 set 1
	}
}

doors_keycard
{
	spawn
	{
		setstate alliesteamdoor2 invisible
		setstate alliesteamdoor3 invisible
	}

	trigger active
	{
		setstate alliesteamdoor2 default	// real
		setstate allies_door_2 invisible	// fake

		setstate alliesteamdoor3 default	// real
		setstate allies_door_3 invisible	// fake

		trigger door_key_1a open
		trigger door_key_2a open
	}

	// currently this routine is never executed
	trigger disable
	{
		setstate alliesteamdoor2 invisible
		setstate allies_door_2 default

		trigger door_key_1a close
		trigger door_key_2a close
	}
}

doors_sabotage
{
	spawn
	{
		setstate doors_sabotage invisible

		accum 1 set 0
		accum 2 set 0
		accum 3 set 0
	}

	trigger allies
	{
		accum 1 abort_if_not_equal 1

		trigger door_key_1a open
		trigger door_key_2a open
		trigger door_key_3a open

		wm_announce "^3Warning: ^1Door controls ^7system ^2rebooted^7!"
		alertentity sabotage_alert

		trigger allies_door_2 open
		trigger allies_door_3 open

		accum 3 set 0	//axis may click now
		accum 1 set 0	//no-repeat

		setstate doors_sabotage invisible
		wait 5000
		setstate doors_sabotage default
	}

	trigger axis
	{
		accum 3 abort_if_not_equal 0

		trigger door_key_1a close
		trigger door_key_2a close
		trigger door_key_3a close

		wm_announce "^3Warning: ^1Door controls ^7system ^1sabotaged^7!"
		alertentity sabotage_alert

		trigger allies_door_2 close
		trigger allies_door_3 close

		accum 1 set 1	//allies may click now
		accum 3 set 1	//no-repeat

		setstate doors_sabotage invisible
		wait 5000
		setstate doors_sabotage default
	}

	activate allies
	{
		trigger doors_sabotage allies
	}

	activate axis
	{
		trigger doors_sabotage axis
	}
}

door_key_1a
{
	spawn
	{
	}

	trigger open
	{
		playsound sound/movers/doors/door4_open.wav volume 100
		faceangles 0 90 0 1000
	}

	trigger close
	{
		playsound sound/movers/doors/door4_close.wav volume 100
		faceangles 0 0 0 1000
		playsound sound/movers/doors/door4_endc.wav volume 100
	}
}

door_key_2a
{
	spawn
	{
	}

	trigger open
	{
		playsound sound/movers/doors/door4_open.wav volume 100
		faceangles 0 -90 0 1000
	}

	trigger close
	{
		playsound sound/movers/doors/door4_close.wav volume 100
		faceangles 0 0 0 1000
		playsound sound/movers/doors/door4_endc.wav volume 100
	}
}

enable_safe
{
	death
	{
		// *----------------------------------- vo ----*
		//wm_teamvoiceannounce 0 "dubrovnik_axis_obj_captured"
		//wm_teamvoiceannounce 1 "dubrovnik_allies_obj_secured"
		//wm_teamvoiceannounce 0 "dubrovnik_axis_protect_gold"
		//wm_teamvoiceannounce 1 "dubrovnik_allies_steal_gold"
		// *----------------------------------- vo ----*

		enablespeaker generator_alarm
		wait 10000
		disablespeaker generator_alarm

		trigger self remove
	}

	trigger remove
	{
		remove
	}
}

axisteamdoor1
{
	trigger kill
	{
		remove
	}
}

// Igloo: tunnel doors

ldoor_tunnel
{
	spawn
	{
	}

	trigger go_up
	{
		wait 500
		playsound sound/movers/misc/garage_door_start_01.wav
		wait 400
		playsound sound/movers/misc/garage_door_loop_01.wav looping forever
		gotomarker ldoor_tunnel_path 30 wait
		stopsound
		playsound sound/movers/misc/garage_door_end_01.wav
	}

	trigger go_down
	{
		wait 500
		playsound sound/movers/misc/garage_door_start_01.wav
		wait 400
		playsound sound/movers/misc/garage_door_loop_01.wav looping forever
		gotomarker ldoor_tunnel_main 30 wait
		stopsound
		playsound sound/movers/misc/garage_door_end_01.wav
	}
}

rdoor_tunnel
{
	spawn
	{
	}

	trigger go_up
	{
		wait 500
		playsound sound/movers/misc/garage_door_start_01.wav
		wait 400
		playsound sound/movers/misc/garage_door_loop_01.wav looping forever
		gotomarker rdoor_tunnel_path 30 wait
		stopsound
		playsound sound/movers/misc/garage_door_end_01.wav
	}

	trigger go_down
	{
		wait 500
		playsound sound/movers/misc/garage_door_start_01.wav
		wait 400
		playsound sound/movers/misc/garage_door_loop_01.wav looping forever
		gotomarker rdoor_tunnel_main 30 wait
		stopsound
		playsound sound/movers/misc/garage_door_end_01.wav
	}
}

// Igloo: gold's case

case1
{
	spawn
	{
		wait 200
		setstate truck_gold invisible
		setstate case1_cm_marker default

		globalaccum 8 set 2
	}

	trigger stolen
	{
		globalaccum 8 set 1
		setstate case1_cm_marker invisible

		trigger courtyardspawn enable
		setautospawn "Courtyard" 0
	}

	trigger returned
	{
		globalaccum 8 set 2
		setstate case1_cm_marker default

		trigger courtyardspawn disable
		setautospawn "North Market" 0
	}

	trigger captured
	{
		//setstate truck_gold_trans invisible
		setstate truck_gold default

		wm_objective_status 6 0 2
		wm_objective_status 6 1 1
	}
}

truck_exitpoint
{
	spawn
	{
		wait 100
		setstate truck_exitpoint invisible
	}

	death
	{
		trigger game_manager stolen_case
	}
}

gate3_allied
{
	spawn
	{
		wait 200
		constructible_class 2
		trigger self startup
	}

	built final
	{
		setstate gate3_mat invisible
		setstate gate3_allied_hurt default

		wm_announce "^5Allied team have constructed the Wire Swing Gate"
	}

	decayed final
	{
		trigger self startup
	}

	death
	{
		trigger self startup

		wm_announce "^1Axis team have destroyed the Wire Swing Gate"
	}

	trigger startup
	{
		setstate gate3_mat default
		setstate gate3_allied_hurt invisible
	}
}

gold_door
{
	spawn
	{
		constructible_class 3
	}

	death
	{
		wm_announce "^5Allies have destroyed the Bank's entrance"

		setstate bank_barricade_toi default
		trigger bank_barricade setup

		wm_objective_status 5 0 2
		wm_objective_status 5 1 1
	}
}

axisteamdoor3
{
	trigger kill
	{
		remove
	}
}

bank_barricade
{
	spawn
	{
		wait 400

		accum 2 set 0

		setstate bank_barricade_materials invisible
		setstate bank_barricade_materials_clip invisible
		setstate bank_barricade_flag invisible
		setstate bank_barricade invisible

		setstate bank_barricade_door invisible
		setstate box_move_1 invisible
		setstate box_move_2 invisible

		setstate bank_barricade_toi invisible

 		constructible_class 2
	}

	trigger setup
	{
		setstate bank_barricade_materials default
		setstate bank_barricade_materials_clip default
		setstate bank_barricade_flag default

		setstate bank_barricade_door invisible
	}

	buildstart final
	{
		setstate bank_barricade_materials default
		setstate bank_barricade_materials_clip default
		setstate bank_barricade_flag default

		setstate bank_barricade_door underconstruction

		accum 2 abort_if_equal 1

		setstate box_move_1 underconstruction
		setstate box_move_2 underconstruction

		setstate crate_lines underconstruction
	}

	built final
	{
		setstate bank_barricade_materials invisible
		setstate bank_barricade_materials_clip invisible
		setstate bank_barricade_flag invisible

		setstate bank_barricade_door default

		setstate box_move_1 default
		setstate box_move_2 default

		setstate crate_lines default

		wm_announce "^1Axis have built the Bank's barricade"

		accum 2 set 1	// the boxes and their lines are built forever
	}

	decayed final
	{
		trigger self setup
	}

	death
	{
		trigger self setup
		wm_announce "^5Allies have destroyed the Bank's barricade"
	}
}

fan1
{
	spawn
	{
		wait 100
		setrotation	0 50 0
	}
}

fan2
{
	spawn
	{
		wait 100
		setrotation	0 75 0
	}
}

// Igloo: back door

backdoor
{
	spawn
	{
		wait 100
		constructible_class 3
	}

	death
	{
		trigger backdoor_after blast
		//trigger axisteamdoor5 kill
	}
}

backdoor_after
{
	spawn
	{
		wait 100
		setstate backdoor_after invisible
	}

	trigger blast
	{
		setstate backdoor_after default
		wm_announce "^5Allies have destroyed the Back door!"

		setstate barricademark default
		trigger barricade setup
	}
}

//axisteamdoor5
//{
//	trigger kill
//	{
//		remove
//	}
//}

barricade
{
	spawn
	{
		wait 400

 		constructible_class 2
		constructible_chargebarreq 0.8
		//constructible_health 300

		constructible_constructxpbonus 4

		setstate barricade_materials invisible
		setstate barricade_materials_clip invisible
		setstate barricade_flag invisible
		setstate barricade invisible
		setstate barricademark invisible
	}

	trigger setup
	{
		setstate barricade_materials default
		setstate barricade_materials_clip default
		setstate barricade_flag default
		setstate barricade invisible
	}

	buildstart final
	{
		setstate barricade_materials default
		setstate barricade_materials_clip default
		setstate barricade_flag default
		setstate barricade underconstruction
	}

	built final
	{
		setstate barricade_materials invisible
		setstate barricade_materials_clip invisible
		setstate barricade_flag invisible
		setstate barricade default
		wm_announce "^1Axis have built the Backdoor's barricade"
	}

	decayed final
	{
		trigger self setup
	}

	death
	{
		trigger self setup
		wm_announce "^5Allies have destroyed the Backdoor's barricade"
	}
}

axisteamdoor2a
{
	spawn
	{
	}

	trigger close
	{
		set
		{
			key "-1"
		}
	}
}

door_key_3a
{
	spawn
	{
	}

	trigger open
	{
		set
		{
			key "0"
		}
	}

	trigger close
	{
		set
		{
			key "-1"
		}
	}
}

allies_door_2
{
	spawn
	{
	}

	trigger open
	{
		set
		{
			key "0"
		}
	}

	trigger close
	{
		set
		{
			key "-1"
		}
	}
}

allies_door_3
{
	spawn
	{
	}

	trigger open
	{
		set
		{
			key "0"
		}
	}

	trigger close
	{
		set
		{
			key "-1"
		}
	}
}